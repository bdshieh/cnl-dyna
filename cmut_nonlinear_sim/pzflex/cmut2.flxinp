c SINGLE MATERIAL CMUT K-MATRIX STATIC ANALYSIS

c ** PARAMETERS **
symb x_len = 40e-6
symb y_len = 40e-6
symb thickness = 2e-6
symb freqint = 1e6
symb x_load = 5e-6
symb y_load = 5e-6
symb dx = 1e-6
symb dy = 1e-6
symb xymin = $x_len / 30
symb zin = $thickness / 8

c ** GEOMETRY **
symb xx1 = -$x_len / 2
symb xx2 = $x_load - $dx / 2
symb xx3 = $x_load + $dx / 2
symb xx4 = $x_len / 2
symb #keycordt x 1 $xx1 $xx2 $xx3 $xx4

symb yy1 = -$y_len / 2
symb yy2 = $y_load - $dy / 2
symb yy3 = $y_load + $dy / 2
symb yy4 = $y_len / 2
symb #keycordt y 1 $yy1 $yy2 $yy3 $yy4

symb zz1 = 0
symb zz2 = $thickness / 2
symb zz3 = $thickness
symb #keycordt z 1 $zz1 $zz2 $zz3

symb #get { idx } rootmax x
symb #get { jdx } rootmax y
symb #get { kdx } rootmax z

symb #keyindx i 1 $idx 1 $xymin 1
symb #keyindx j 1 $jdx 1 $xymin 1
symb #keyindx k 1 $kdx 1 $zin 1 
symb indgrd = $i$idx
symb jndgrd = $j$jdx
symb kndgrd = $k$kdx

grid $indgrd $jndgrd $kndgrd
geom keypnt $idx $jdx $kdx

c ** MATERIALS **
matr
	type elas
	wvsp on
	prop sin 2200 11000 6250
c 	mdmp sin 1e6 db 4000

c ** MATERIAL ASSIGNMENT **
site regn sin

c ** BOUNDARY CONDITIONS **
boun
	side xmin fixd
	side xmax fixd
	side ymin fixd
	side ymax fixd

c ** PRESSURE LOADING **
c func astep 1 * 0 0 cos 1 100e-9 0 *
func step 1 0 0 0

plod
	pdef pld1 func 1e9
	vctr vct1 0 0 1
	sdef pld1 vct1 $i2 $i3 $j2 $j3 $k2 $k2

c ** CALCULATIONS **
calc disp

c ** SOLVING **
prcs

symb #get { step } timestep 
symb tstop = 20e-6
symb nstep = $tstop / $step

c dynamic solver with a ton of damping and ringdown termination
c exec $nstep
c exec ringdown $nstep

c static solver with auto dynamic relaxation 
drlx auto
exec $nstep $tstop stat

c ** DATA OUT **
data
	out modl
	out xdsp
	out ydsp
	out zdsp
	out zfrc

data
	file out zdsp.mat 
	form out matlab
	out zdsp

c ** PLOTS **
c plots
c grph
c	nvew 2 2
c	plot 1
c	plot 2
c term

	